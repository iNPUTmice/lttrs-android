apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"
apply plugin: "com.diffplug.spotless"

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "rs.ltt.android"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 14
        versionName "0.3.3"

        setProperty("archivesBaseName", "Ltt.rs-$versionName")

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'


        buildConfigField "boolean", "USE_FOREGROUND_SERVICE", "true"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    flavorDimensions "distribution"

    productFlavors {

        fdroid {
            dimension "distribution"
        }

        playstore {
            dimension "distribution"
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "USE_FOREGROUND_SERVICE", "false"
        }
    }

    buildFeatures {
        dataBinding true
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
        }
    }
    lint {
        disable 'MissingTranslation'
    }
    namespace 'rs.ltt.android'


}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat('1.8').aosp().reflowLongStrings()
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'

    //AndroidX
    implementation "androidx.appcompat:appcompat:$rootProject.ext.appcompatVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.ext.lifecycleVersion"

    implementation "androidx.navigation:navigation-fragment:$rootProject.ext.navVersion"
    implementation "androidx.navigation:navigation-ui:$rootProject.ext.navVersion"

    implementation "androidx.paging:paging-common:$rootProject.ext.pagingVersion"
    implementation "androidx.paging:paging-runtime:$rootProject.ext.pagingVersion"


    implementation "androidx.room:room-runtime:$rootProject.ext.roomVersion"
    implementation "androidx.room:room-guava:$rootProject.ext.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.ext.roomVersion"

    implementation "androidx.recyclerview:recyclerview:$rootProject.ext.recyclerViewVersion"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.ext.swipeRefreshLayoutVersion"

    implementation "androidx.work:work-runtime:$rootProject.ext.workVersion"

    implementation "androidx.exifinterface:exifinterface:$rootProject.ext.exifInterfaceVersion"


    //Google
    implementation "com.google.android.material:material:$rootProject.ext.material"

    //Google - Firebase Messaging
    playstoreImplementation('com.google.firebase:firebase-messaging:23.1.0') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }


    //Third party
    implementation 'org.hsluv:hsluv:0.2'

    implementation 'com.github.tony19:logback-android:2.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'


    //Own libraries (libraries we wrote and can commit to)
    implementation("rs.ltt.jmap:jmap-mua:$rootProject.ext.muaVersion") {
        changing = true
    }

    implementation("rs.ltt.autocrypt:autocrypt-jmap:$rootProject.ext.autocryptJmapVersion") {
        changing = true
    }


    //Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestImplementation("rs.ltt.jmap:jmap-mock-server:$rootProject.ext.muaVersion") {
        changing = true
    }
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
}
